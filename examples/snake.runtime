/ Snake
/ by Siwei
/ Jan 2019

/ -- initialize --
let width 24
let height 24

let snake []

let snake_x 10
#init_snake
let snake_seg []
psh snake_seg $snake_x
psh snake_seg 12
psh snake $snake_seg
add snake_x $snake_x 1
if {$snake_x==13} _ init_snake

let direction 2
let len 3

let x 12
let y 12

/ -- print info --
prt {'Press arrow keys to control.'}

#apple
rnd apple_x 0 $width
rnd apple_y 0 $height
pixel color $apple_x $apple_y
if {$color==1} apple
drw $apple_x $apple_y 1

#begin
slp 200

let key {$lastkey}
if {$key==37 && $direction!=2} press_left
if {$key==38 && $direction!=3} press_up
if {$key==39 && $direction!=0} press_right
if {$key==40 && $direction!=1} press_down
jmp move

#press_left
let direction 0
jmp move
#press_up
let direction 1
jmp move
#press_right
let direction 2
jmp move
#press_down
let direction 3
jmp move

#move
if {$direction==0} move_left
if {$direction==1} move_up
if {$direction==2} move_right
if {$direction==3} move_down

#move_left
sub x $x 1
if {$x<0} _ move_snake
sub x $width 1
jmp move_snake
#move_right
add x $x 1
if {$x==$width} _ move_snake
let x 0
jmp move_snake
#move_up
sub y $y 1
if {$y<0} _ move_snake
sub y $height 1
jmp move_snake
#move_down
add y $y 1
if {$y==$height} _ move_snake
let y 0
jmp move_snake

#move_snake
let new_head []
psh new_head $x
psh new_head $y
psh snake $new_head
if {$x==$apple_x&&$y==$apple_y} _ miss_apple
add len $len 1
prt {'+1'}
jmp apple

#miss_apple
pxl color $x $y
if {$color==1} game_over

drw $x $y 1

pol snake tail
pop tail tail_y
pop tail tail_x
drw $tail_x $tail_y 0

jmp begin

#game_over
prt {'Game Over'}
prt {'SCORE: '+$len}