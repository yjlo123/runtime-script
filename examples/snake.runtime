/ Snake
/ by Siwei
/ Jan 2019

/ -- initialize --
let width 24
let height 24

let snake []

let snake_x 10
#init_snake
let snake_seg []
push snake_seg $snake_x
push snake_seg 12
push snake $snake_seg
add snake_x $snake_x 1
if {$snake_x==13} _ init_snake

let direction 2
let len 3

let x 12
let y 12

/ -- print info --
print {'Press arrow keys to control.'}

#apple
random apple_x 0 $width
random apple_y 0 $height
pixel color $apple_x $apple_y
if {$color==1} apple
draw $apple_x $apple_y 1

#begin

sleep 200

let key {$lastkey}
if {$key==37 && $direction!=2} press_left
if {$key==38 && $direction!=3} press_up
if {$key==39 && $direction!=0} press_right
if {$key==40 && $direction!=1} press_down
jump move

#press_left
let direction 0
jump move
#press_up
let direction 1
jump move
#press_right
let direction 2
jump move
#press_down
let direction 3
jump move

#move
if {$direction==0} move_left
if {$direction==1} move_up
if {$direction==2} move_right
if {$direction==3} move_down

#move_left
sub x $x 1
if {$x<0} _ move_snake
let x {$width-1}
jump move_snake
#move_right
add x $x 1
if {$x==$width} _ move_snake
let x 0
jump move_snake
#move_up
sub y $y 1
if {$y<0} _ move_snake
let y {$height-1}
jump move_snake
#move_down
add y $y 1
if {$y==$height} _ move_snake
let y 0
jump move_snake

#move_snake
push snake {[$x, $y]}
if {$x==$apple_x&&$y==$apple_y} _ miss_apple
add len $len 1
print {'+1'}
jump apple

#miss_apple
pixel color $x $y
if {$color==1} game_over

draw $x $y 1

poll snake tail
draw $tail[0] $tail[1] 0

jump begin

#game_over
print {'Game Over'}
print {'SCORE: '+$len}
